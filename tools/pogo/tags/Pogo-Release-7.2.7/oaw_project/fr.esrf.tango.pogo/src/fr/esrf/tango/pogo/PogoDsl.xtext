grammar fr.esrf.tango.pogo.PogoDsl with org.eclipse.xtext.common.Terminals

generate pogoDsl "http://www.esrf.fr/tango/pogo/PogoDsl"

PogoSystem:
	(imports+=Import)*
	(classes+=PogoDeviceClass)*
	(multiClasses+=PogoMultiClasses)*;

Import:
	"import" importURI=STRING;
//==============================================
//	Multi Classes definition
//==============================================
PogoMultiClasses:
	"multiclasses" name=ID
	"{"
		sourcePath  = STRING
		description = STRING
		title       = STRING
		"classes:"
	    	(classes+=OneClassSimpleDef)
	
		filestogenerate = STRING			//	File(s) to generate (code, makefile,....)
		preferences     = Preferences		//	Preferences (for programer, for site or at run time)
	"}"
;

//
//	One class simple definition
//
OneClassSimpleDef:
	classname  = STRING
	sourcePath = STRING
	pogo6      = Boolean
	"inheritances:"
	   	(inheritances+=Inheritance)			//	inheritance class definitions
	"parentClasses:"
		(parentClasses+=STRING)	//	Used only by graphical display
	"additionalFiles:"
		(additionalFiles+=AdditionalFile)*	//	Programmer's additional files to be added in Makefile (utils, threads,...)
;

//==============================================
//	Class definition
//==============================================
PogoDeviceClass:
	"deviceclass" name=ID (isAbstract?="abstract")? ("extends" baseClass=[PogoDeviceClass])?
	"{"
		institute = ID							//	For future specific features 

		"description:"							//	Class information
			(description=ClassDescription)

		"classProperties:"						//	Class property list
			(classProperties+=Property)*

		"deviceProperties:"						//	Device Property list
			(deviceProperties+=Property)*

		"commands:"								//	Device command list
			(commands+=Command)*

		"attributes:"							//	Device attribute list
			(attributes+=Attribute)*

		"states:"								//	Device state list
			(states+=State)*
		
		preferences = Preferences				//	Preferences (for programer, for site or at run time)

		"additionalFiles:"
			(additionalFiles+=AdditionalFile)*	//	Programmer's additional files to be added in Makefile (utils, threads,...)
	"}";

//
//	Miscellaneous definitions
//
Language:		"Cpp"      | "Java"     | "Python";
DisplayLevel:	"OPERATOR" | "EXPERT";
AttrType:		"Scalar"   | "Spectrum" | "Image";
RW_Type:		"READ"     | "WRITE"    | "READ_WRITE" | "READ_WITH_WRITE";
Boolean:		"true"     | "false";


//
//	Class information
//
ClassDescription:
	description    = STRING	//	Class description
	title          = STRING	//	Short description
	sourcePath     = STRING	//	Files location
	"inheritances:"
	    (inheritances+=Inheritance)	//	inheritance class definitions
	language       = Language		//	Language to generate
	filestogenerate= STRING			//	File(s) to generate (code, makefile,....)
	identification = ClassIdentification
	comments       = Comments
	hasMandatoryProperty  = Boolean
	hasAbstractCommand    = Boolean
	hasAbstractAttribute  = Boolean
	hasDynamicAttribute   = Boolean
	descriptionHtmlExists = Boolean	//	File from pogo-6
;
	
//
//	Inheritance Definition
//
Inheritance:
	classname  = STRING
	sourcePath = STRING;


//
//	Class identification
//
ClassIdentification:
	contact      = STRING	//	email address of the person in charge
	author       = STRING	//	get from contact
	emailDomain  = STRING	//	get from contact

	classFamily  = STRING	//	class family to be sorted in html doc
	siteSpecific = STRING	//	If specific for an institute, name of this institute
	platform     = STRING	//	OS where it can run
	bus          = STRING	//	Hardware bus to control.
	manufacturer = STRING	//	mannufacturer if any
	reference    = STRING;	//	product reference if any 

//
//	Comments Definition (code generated by java Pogo and not by xtext/xpand)
//
Comments:
	commandsTable   = STRING;

//
//	Preferences (for programer or for site)
//
Preferences:
	docHome       =  STRING
	makefileHome  =  STRING
	installHome   =  STRING
	htmlVersion   =  Boolean;	//	Used by web automat to manage tag version

//
//	State Definition
//
State:
	name        = ID
	description = STRING
	status      = InheritanceStatus;

//
//	Property Definition
//
Property:
	name        = ID
	type        = PropType
	status      = InheritanceStatus
	mandatory   = Boolean
	description = STRING
	"defaultPropValue:"
		(DefaultPropValue+=STRING)*;

//	types could be one of the types
PropType:
	SimpleType | VectorType;
	
SimpleType:
	BooleanType | ShortType  | UShortType  |
	IntType     | UIntType   | FloatType   |
	DoubleType  | StringType;

VectorType:	
	ShortVectorType |
	IntVectorType   | FloatVectorType  |
	DoubleVectorType| StringVectorType;


InheritanceStatus:
	abstract     = Boolean	//	is abstract or inherited from abstract
	inherited    = Boolean	//	is hinerited
	concrete     = Boolean	//	is concrete or overloaded
	concreteHere = Boolean	//	is concrete or overloaded in this class
	hasChanged   = STRING;	//	What has changed (dataType, attType, RWtype)


//
//	Command Definition
//
Command:
		name         = ID
		argin        = Argument
		argout       = Argument 
		description  = STRING
		status       = InheritanceStatus
		execMethod   = STRING 
		displayLevel = DisplayLevel
		polledPeriod = STRING
		"excludedStates:"
			(excludedStates+=STRING)*;

Argument:
	type=Type description=STRING; 

//
//	Attribute Definition
//
Attribute:
	name             = ID
	attType          = AttrType
	dataType         = Type
	rwType           = RW_Type
	displayLevel     = DisplayLevel
	polledPeriod     = STRING
	maxX             = STRING
	maxY             = STRING
	associatedAttr   = STRING
	memorized        = Boolean
	memorizedAtInit  = Boolean
	changeEvent      = FireEvents
	archiveEvent     = FireEvents
	dataReadyEvent   = FireEvents
	status           = InheritanceStatus
	properties       = AttrProperties
	allocReadMember  = Boolean
	isDynamic        = Boolean
	
	"readExcludedStates:"
		(readExcludedStates+=STRING)*
	"writeExcludedStates:"
		(writeExcludedStates+=STRING)*;

FireEvents:
	fire             = Boolean
	libCheckCriteria = Boolean;

//
//	Attribute Properties Definition
//
AttrProperties:
	description   = STRING
	label         = STRING
	unit          = STRING
	standardUnit  = STRING
	displayUnit   = STRING
	format        = STRING
	maxValue      = STRING
	minValue      = STRING
	maxAlarm      = STRING
	minAlarm      = STRING
	maxWarning    = STRING
	minWarning    = STRING
	deltaTime     = STRING
	deltaValue    = STRING;

//
//	Additional files (not Tango classes)
//
AdditionalFile:
	name   = STRING
	path   = STRING;


//
//	types could be one of the types
//
Type:
	VoidType   | BooleanType |
	ShortType  | UShortType  |
	IntType    | UIntType    |
	FloatType  | DoubleType  |
	StringType |
	
	CharArrayType  | ShortArrayType | UShortArrayType |
	IntArrayType   | UIntArrayType  | FloatArrayType  |
	DoubleArrayType| StringArrayType| LongStringArrayType |
	DoubleStringArrayType |
	
	StateType      | ConstStringType| BooleanArrayType |
	LongType       | ULongType      | UCharType        |
	LongArrayType  | ULongArrayType |
	DevIntType     | EncodedType;
	
//
//	Tango type defined by IDL.
//
VoidType:				{VoidType} "void";
BooleanType:			{BooleanType} "boolean";
ShortType:				{ShortType} "short";
UShortType:				{UShortType} "ushort";
IntType:				{IntType} "int";
UIntType:				{UIntType} "uint";
FloatType:				{FloatType} "float";
DoubleType:				{DoubleType} "double";
StringType:				{StringType} "string";

CharArrayType:			{CharArrayType} "DevVarCharArray";
ShortArrayType:			{ShortArrayType} "DevVarShortArray";
UShortArrayType:		{UShortArrayType} "DevVarUShortArray";
IntArrayType:			{IntArrayType} "DevVarLongArray";
UIntArrayType:			{UIntArrayType} "DevVarULongArray";
FloatArrayType:			{FloatArrayType} "DevVarFloatArray";
DoubleArrayType:		{DoubleArrayType} "DevVarDoubleArray";
StringArrayType:		{StringArrayType} "DevVarStringArray";
LongStringArrayType:	{LongStringArrayType} "DevVarLongStringArray";
DoubleStringArrayType:	{DoubleStringArrayType} "DevVarDoubleStringArray";
StateType:				{StateType} "DevState";
ConstStringType:		{ConstStringType} "ConstDevString";
BooleanArrayType:		{BooleanArrayType} "DevVarBooleanArray";
UCharType:				{UCharType} "DevUChar";

LongType:				{LongType} "DevLong64";
ULongType:				{ULongType} "DevULong64";
LongArrayType:			{LongArrayType} "DevVarLong64Array";
ULongArrayType:			{ULongArrayType} "DevVarULong64Array";
DevIntType:				{DevIntType} "DevInt";
EncodedType:			{EncodedType} "DevEncoded";

ShortVectorType:		{ShortVectorType} 	"vector<short>";
IntVectorType:			{IntVectorType} 	"vector<int>";
FloatVectorType:		{FloatVectorType} 	"vector<float>";
DoubleVectorType:		{DoubleVectorType} 	"vector<double>";
StringVectorType:		{StringVectorType} 	"vector<string>";

